plugins {
    id "org.jetbrains.kotlin.jvm"
    id "com.google.cloud.tools.jib" version "1.2.0"
    id "io.spring.dependency-management"
    id "me.champeau.gradle.jmh" version "0.4.8"
    id "org.springframework.boot"
}

version = "0.0.1"

configurations {
    implementation {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    testImplementation {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
}

dependencies {
    // module dependencies
    implementation(project(":name-score-calculator"))

    // library dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-log4j2:${springBootVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"

    // test frameworks
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    testImplementation "org.springframework.boot:spring-boot-test:${springBootVersion}"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs += "-Xjsr305=strict"
        jvmTarget = "12"
        suppressWarnings = false
    }
}

bootRun {
    standardInput = System.in
    jvmArgs = ["-Xverify:none"] // startup optimization
}

jmh {
    humanOutputFile = null
    warmupIterations = 1
    iterations = 1
}